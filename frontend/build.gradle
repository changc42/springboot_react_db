buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

plugins {
    id "base"
    id "com.github.node-gradle.node" version "3.0.1"
}

node {
    version = '14.16.1'
    npmVersion = '7.10.0'
    download = true
}

npm_run_build {
    inputs.files fileTree("public")
    inputs.files fileTree("src")

    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    outputs.dir 'build'
}

task packageFrontend(type: Zip) {
    dependsOn npm_run_build
    baseName 'frontendZip'
    extension 'jar'
    destinationDir file("${projectDir}/build_packageFrontend")
    from('build') {
        // optional path under which output will be visible in Java classpath, e.g. static resources path
        into 'static'
    }
}

assemble.dependsOn packageFrontend

configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageFrontend.archivePath) {
        builtBy packageFrontend
        type "jar"
    }
}

clean {
    delete packageFrontend.archivePath
}